apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

apply from: '../values.gradle'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "sgtmelon.scriptum"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 20
        versionName git.version.windows.execute().text.trim()

        testInstrumentationRunner androidx.test.instrumentationRunner

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "String", "DB_NAME", '"HandyNotes"'
        buildConfigField "String", "MARKET_URL", '"market://details?id="'
        buildConfigField "String", "BROWSER_URL", '"https://play.google.com/sore/apps/details?id="'
        buildConfigField "String", "PRIVACY_POLICY_URL", '"https://drive.google.com/open?id=1f5tfXeBM6dxUqpsHW3SqMHa-mtKf5RSx"'

//        multiDexEnabled true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "int", "INPUT_CONTROL_MAX_SIZE", '200'
        }

        debug {
            /**
             * Turn off for tests correct work!
             */
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'proguard-test-rules.pro'

            buildConfigField "int", "INPUT_CONTROL_MAX_SIZE", '50'
        }
    }
}

dependencies {
    implementation project(":iconanim")
    implementation project(":safedialog")
    implementation project(":extension")

    //region Kotlin

    implementation jetbrains.kotlin
    implementation jetbrains.coroutines.core
    implementation jetbrains.coroutines.android

    //endregion

    implementation google.firebase.analytics
    implementation google.firebase.crashlytics

    implementation google.material

    implementation google.dagger.core
    kapt google.dagger.compiler

    //region AndroidX

    implementation androidx.core

    implementation androidx.lifecycle.extensions
    implementation androidx.lifecycle.viewmodel
    kapt androidx.lifecycle.compiler

    implementation androidx.room.runtime
    implementation androidx.room.ktx
    kapt androidx.room.compiler
    testImplementation androidx.room.testing

    implementation androidx.preference
    implementation androidx.appcompat
    implementation androidx.cardview
    implementation androidx.constraintlayout

    implementation androidx.vector.drawable
    implementation androidx.vector.animated

    //endregion

    implementation other.pageindicator

    //region Testing

//    debugImplementation other.leakcanary

    androidTestImplementation androidx.test.runner
    androidTestImplementation androidx.test.rules
    androidTestImplementation androidx.test.junit

    debugImplementation androidx.test.espresso.idling
    androidTestImplementation androidx.test.espresso.core
    androidTestImplementation androidx.test.espresso.contrib
    androidTestImplementation androidx.test.uiautomator

    debugImplementation junit.core
    debugImplementation jetbrains.coroutines.test

    testImplementation mockk.core
    testImplementation json.core

    //endregion
    
}