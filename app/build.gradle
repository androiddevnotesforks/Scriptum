plugins {
    id "com.android.application"
    id 'org.jetbrains.kotlin.android'
    id "kotlin-kapt"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

apply from: '../values.gradle'

android {
    compileSdk 33

    defaultConfig {
        applicationId "sgtmelon.scriptum"
        minSdk 21
        targetSdk 32
        versionCode 23
        versionName git.version.execute().text.trim()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "String", "DB_NAME", '"HandyNotes"'
        buildConfigField "String", "MARKET_URL", '"market://details?id="'
        buildConfigField "String", "BROWSER_URL", '"https://play.google.com/sore/apps/details?id="'
        buildConfigField "String", "PRIVACY_POLICY_URL", '"https://github.com/SerjantArbuz/Scriptum/blob/master/PrivacyPolicy.md"'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "int", "INPUT_CONTROL_MAX_SIZE", '200'
        }

        debug {

            applicationIdSuffix = ".debug"

            /** Turn off for tests correct work! */
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'proguard-test-rules.pro'

            buildConfigField "int", "INPUT_CONTROL_MAX_SIZE", '50'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        dataBinding = true
    }
}

dependencies {
    implementation project(':libraries:iconAnim')
    implementation project(':libraries:safeDialog')
    implementation project(':libraries:textDotAnim')
    implementation project(':libraries:extensions')

    implementation project(':tests:idling')
    implementation project(':tests:prod')
    testImplementation project(path: ':tests:common')
    androidTestImplementation project(path: ':tests:common')
    androidTestImplementation project(path: ':tests:cappuccino')

    //region Kotlin

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"

    // TODO why debugImplementation?
    debugImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0"

    //endregion

    //region Google

    implementation "com.google.firebase:firebase-analytics-ktx:21.1.1"
    implementation "com.google.firebase:firebase-crashlytics-ktx:18.2.13"

    implementation "com.google.android.material:material:1.6.1"

    implementation "com.google.dagger:dagger:2.41"
    kapt "com.google.dagger:dagger-compiler:2.41"

    //endregion

    //region AndroidX

    implementation "androidx.core:core-ktx:1.9.0"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    kapt "androidx.lifecycle:lifecycle-compiler:2.5.1"

    implementation "androidx.room:room-runtime:2.4.3"
    implementation "androidx.room:room-ktx:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"
    testImplementation "androidx.room:room-testing:2.4.3"

    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation "androidx.vectordrawable:vectordrawable-animated:1.1.0"

    //endregion

    implementation "ru.tinkoff.scrollingpagerindicator:scrollingpagerindicator:1.2.1"

    //region Testing

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.6"

    androidTestImplementation "androidx.test:core-ktx:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"

    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.4.0"

    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.2.0"

    // TODO really need it?
    // Exclude need for prevent bug: import org.hamcrest.Matchers - not found
    debugImplementation ("androidx.test.ext:junit:1.1.3") {
        exclude group: 'org.hamcrest'
    }

    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "io.mockk:mockk:1.12.4"
    testImplementation "org.json:json:20211205"

    //endregion

    implementation 'com.jakewharton.timber:timber:5.0.1'

}